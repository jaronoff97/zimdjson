const std = @import("std");
const builtin = @import("builtin");
const types = @import("types.zig");
const cpu = builtin.cpu;
const simd = std.simd;
const umask = types.umask;
const vector = types.vector;
const Vector = types.Vector;

pub inline fn clmul(quotes_mask: umask) umask {
    if (comptime cpu.arch == .x86_64) {
        const ones: @Vector(16, u8) = @splat(0xFF);
        return asm (
            \\vpclmulqdq $0, %[ones], %[quotes], %[ret]
            : [ret] "=x" (-> umask),
            : [ones] "x" (ones),
              [quotes] "x" (quotes_mask),
        );
    } else {
        var bitmask = quotes_mask;
        bitmask ^= bitmask << 1;
        bitmask ^= bitmask << 2;
        bitmask ^= bitmask << 4;
        bitmask ^= bitmask << 8;
        bitmask ^= bitmask << 16;
        bitmask ^= bitmask << 32;
        return bitmask;
    }
}

pub inline fn lookupTable(table: vector, nibbles: vector) vector {
    if (comptime cpu.arch == .x86_64) {
        return asm (
            \\vpshufb %[nibbles], %[table], %[ret]
            : [ret] "=x" (-> vector),
            : [table] "x" (table),
              [nibbles] "x" (nibbles),
        );
    } else if (comptime cpu.arch == .aarch64) {
        return asm (
            \\tbl %[ret].16b, {%[table].16b}, %[nibbles].16b
            : [ret] "=w" (-> vector),
            : [table] "w" (table),
              [nibbles] "w" (nibbles),
        );
    } else {
        @compileError("Intrinsic not implemented for this target");
    }
}

// only used in x86_64
pub inline fn pack(vec1: @Vector(4, i32), vec2: @Vector(4, i32)) @Vector(8, u16) {
    if (comptime cpu.arch == .x86_64) {
        return asm (
            \\vpackusdw %[vec1], %[vec2], %[ret]
            : [ret] "=x" (-> @Vector(8, u16)),
            : [vec1] "x" (vec1),
              [vec2] "x" (vec2),
        );
    } else {
        @compileError("Intrinsic not implemented for this target");
    }
}

//  only used in x86_64
pub inline fn mulSaturatingAdd(vec1: @Vector(16, u8), vec2: @Vector(16, u8)) @Vector(8, u16) {
    if (comptime cpu.arch == .x86_64) {
        return asm (
            \\vpmaddubsw %[vec1], %[vec2], %[ret]
            : [ret] "=x" (-> @Vector(8, u16)),
            : [vec1] "x" (vec1),
              [vec2] "x" (vec2),
        );
    } else {
        @compileError("Intrinsic not implemented for this target");
    }
}

// only used in x86_64
pub inline fn mulWrappingAdd(vec1: @Vector(8, i16), vec2: @Vector(8, i16)) @Vector(4, i32) {
    if (comptime cpu.arch == .x86_64) {
        return asm (
            \\vpmaddwd %[vec1], %[vec2], %[ret]
            : [ret] "=x" (-> @Vector(4, i32)),
            : [vec1] "x" (vec1),
              [vec2] "x" (vec2),
        );
    } else {
        @compileError("Intrinsic not implemented for this target");
    }
}
